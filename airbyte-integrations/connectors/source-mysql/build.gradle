plugins {
    id 'application'
    id 'airbyte-docker'
    id 'airbyte-integration-test-java'
    id 'airbyte-performance-test-java'
    id 'airbyte-connector-acceptance-test'
}

repositories {
    mavenLocal()
}

application {
    mainClass = 'io.airbyte.integrations.source.mysql.MySqlSource'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

dependencies {
    implementation 'io.airbyte:airbyte-cdk:0.0.1-SNAPSHOT'
    implementation project(':airbyte-db:db-lib')
    implementation project(':airbyte-integrations:bases:base-java')
    implementation project(':airbyte-integrations:bases:debezium')
    implementation project(':airbyte-integrations:connectors:source-jdbc')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-integrations:connectors:source-relational-db')

    implementation 'mysql:mysql-connector-java:8.0.30'
    implementation 'org.apache.commons:commons-lang3:3.11'

    testImplementation testFixtures(project(':airbyte-integrations:bases:debezium'))
    testImplementation testFixtures(project(':airbyte-integrations:connectors:source-jdbc'))
    testImplementation 'org.apache.commons:commons-lang3:3.11'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation libs.junit.jupiter.system.stubs
    testImplementation libs.connectors.testcontainers.mysql

    integrationTestJavaImplementation project(':airbyte-integrations:bases:standard-source-test')
    integrationTestJavaImplementation project(':airbyte-integrations:connectors:source-mysql')

    performanceTestJavaImplementation project(':airbyte-integrations:bases:standard-source-test')
    performanceTestJavaImplementation project(':airbyte-integrations:connectors:source-mysql')

    // TODO: remove this once the code has migrated to the CDK
    implementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)
    integrationTestJavaImplementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)
    performanceTestJavaImplementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)
}

task buildCDK(type: Exec) {
    doFirst {
        def isSnapshotCDKVersion = false
        configurations.compileClasspath.incoming.resolutionResult.allDependencies.each { dep ->
            if (dep.requested instanceof ModuleComponentSelector) {
                if (dep.requested.group == 'io.airbyte' && dep.requested.module == 'airbyte-cdk') {
                    if (dep.requested.version.endsWith('-SNAPSHOT')) {
                        isSnapshotCDKVersion = true
                    }
                }
            }
        }
        if (isSnapshotCDKVersion) {
            workingDir "${rootProject.rootDir}"
            commandLine 'sh', '-c', './gradlew :airbyte-cdk:java:airbyte-cdk:publishToMavenLocal'
        } else {
            logger.lifecycle("Not building airbyte-cdk, no snapshot version found.")
        }
    }
}

tasks.named('build').configure {
    dependsOn(buildCDK)
}

tasks.named('test').configure {
    dependsOn(buildCDK)
}