import java.nio.file.Paths

plugins {
    id 'airbyte-docker-bmuschko'
    id 'airbyte-python'
}

airbytePython {
    moduleDirectory 'normalization'
}

dependencies {
    implementation project(':airbyte-commons-worker')
}

// we need to access the sshtunneling script from airbyte-workers for ssh support
task copySshScript(type: Copy, dependsOn: [project(':airbyte-commons-worker').processResources]) {
    from "${project(':airbyte-commons-worker').buildDir}/resources/main"
    into "${buildDir}"
    include "sshtunneling.sh"
}

// make sure the copy task above worked (if it fails, it fails silently annoyingly)
task checkSshScriptCopy(type: Task, dependsOn: copySshScript) {
    doFirst {
        assert file("${buildDir}/sshtunneling.sh").exists() :
                "Copy of sshtunneling.sh failed, check that it is present in airbyte-workers."
    }
}

assemble.dependsOn(checkSshScriptCopy)
test.dependsOn(checkSshScriptCopy)

integrationTest.dependsOn(build)

airbyteDockerBmuschko {
    dockerfileToImageName = [
        'Dockerfile': 'normalization',
        'clickhouse.Dockerfile': 'normalization-clickhouse',
        'mssql.Dockerfile': 'normalization-mssql',
        'mysql.Dockerfile': 'normalization-mysql',
        'oracle.Dockerfile': 'normalization-oracle',
        'redshift.Dockerfile': 'normalization-redshift',
        'snowflake.Dockerfile': 'normalization-snowflake',
        'tidb.Dockerfile': 'normalization-tidb',
    ]
    additionalDependencies = [checkSshScriptCopy]

    // The dbt base image isn't published for arm64 as of 2022-07-28, so only build the amd64 image
    archOverride = 'amd64'
}

task("customIntegrationTestPython", type: PythonTask, dependsOn: installTestReqs) {
    module = "pytest"
    command = "-s integration_tests"

    dependsOn ':airbyte-integrations:bases:base-normalization:buildDockerImage'

    dependsOn ':airbyte-integrations:connectors:destination-bigquery:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-mysql:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-postgres:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-redshift:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-snowflake:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-oracle:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-mssql:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-clickhouse:buildDockerImage'
    dependsOn ':airbyte-integrations:connectors:destination-tidb:buildDockerImage'
}

// not really sure what this task does differently from customIntegrationTestPython, but it seems to also run integration tests
// and as such it depends on the docker images.
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:bases:base-normalization:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-bigquery:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-mysql:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-postgres:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-redshift:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-snowflake:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-oracle:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-mssql:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-clickhouse:buildDockerImage'
project.tasks.findByName('_customIntegrationTestsCoverage').dependsOn ':airbyte-integrations:connectors:destination-tidb:buildDockerImage'

// DATs have some additional tests that exercise normalization code paths,
// so we want to run these in addition to the base-normalization integration tests.
// If you add more items here, make sure to also to have CI fetch their credentials.
// See git history for an example.
// TODO reenable these - they're causing flakiness in our test results, need to figure that out
// integrationTest.dependsOn(":airbyte-integrations:connectors:destination-bigquery:integrationTest")
// integrationTest.dependsOn(":airbyte-integrations:connectors:destination-postgres:integrationTest")
// integrationTest.dependsOn(":airbyte-integrations:connectors:destination-snowflake:integrationTest")

integrationTest.dependsOn("customIntegrationTestPython")
customIntegrationTests.dependsOn("customIntegrationTestPython")

// TODO fix and use https://github.com/airbytehq/airbyte/issues/3192 instead
task('mypyCheck', type: PythonTask) {
    module = "mypy"
    command = "normalization --config-file ${project.rootProject.file('pyproject.toml').absolutePath}"

    dependsOn 'blackFormat'
}
check.dependsOn mypyCheck
