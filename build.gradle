import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:7.2.0'
    }
}

plugins {
    id 'base'
    id 'pmd'
    id 'com.diffplug.spotless' version '6.0.0'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.spotbugs' version '5.0.5'
    id 'version-catalog'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

catalog {
    versionCatalog {
        version('fasterxml_version', '2.13.0')
        version('glassfish_version', '2.31')
        version('commons_io', '2.7')
        version('log4j', '2.17.1')
        version('slf4j', '1.7.30')
        version('lombok', '1.18.22')
        version('junit-jupiter', '5.7.2')
        library('fasterxml', 'com.fasterxml.jackson', 'jackson-bom').versionRef('fasterxml_version')
        library('glassfish', 'org.glassfish.jersey', 'jackson-bom').versionRef('glassfish_version')
        library('jackson-databind', 'com.fasterxml.jackson.core', 'jackson-databind').versionRef('fasterxml_version')
        library('jackson-annotations', 'com.fasterxml.jackson.core', 'jackson-annotations').versionRef('fasterxml_version')
        library('jackson-dataformat', 'com.fasterxml.jackson.dataformat', 'jackson-dataformat-yaml').versionRef('fasterxml_version')
        library('jackson-datatype', 'com.fasterxml.jackson.datatype', 'jackson-datatype-jsr310').versionRef('fasterxml_version')
        library('guava', 'com.google.guava', 'guava').version('30.1.1-jre')
        library('commons-io', 'commons-io', 'commons-io').versionRef('commons_io')
        library('apache-commons', 'org.apache.commons', 'commons-compress').version('1.20')
        library('apache-commons-lang', 'org.apache.commons', 'commons-lang3').version('3.11')
        library('slf4j-api', 'org.slf4j', 'slf4j-api').version('1.7.30')
        library('log4j-api', 'org.apache.logging.log4j', 'log4j-api').versionRef('log4j')
        library('log4j-core', 'org.apache.logging.log4j', 'log4j-core').versionRef('log4j')
        library('log4j-impl', 'org.apache.logging.log4j', 'log4j-slf4j-impl').versionRef('log4j')
        library('log4j-web', 'org.apache.logging.log4j', 'log4j-web').versionRef('log4j')
        library('jul-to-slf4j', 'org.slf4j', 'jul-to-slf4j').versionRef('slf4j')
        library('jcl-over-slf4j', 'org.slf4j', 'jcl-over-slf4j').versionRef('slf4j')
        library('log4j-over-slf4j', 'org.slf4j', 'log4j-over-slf4j').versionRef('slf4j')
        library('appender-log4j2', 'com.therealvan', 'appender-log4j2').version('3.6.0')
        library('aws-java-sdk-s3', 'com.amazonaws', 'aws-java-sdk-s3').version('1.12.6')
        library('google-cloud-storage', 'com.google.cloud', 'google-cloud-storage').version('2.2.2')
        library('s3', 'software.amazon.awssdk', 's3').version('2.16.84')
        library('lombok', 'org.projectlombok', 'lombok').versionRef('lombok')
        library('junit-jupiter-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit-jupiter')
        library('junit-jupiter-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit-jupiter')
        library('junit-jupiter-params', 'org.junit.jupiter', 'junit-jupiter-params').versionRef('junit-jupiter')
        library('mockito-junit-jupiter', 'org.mockito', 'mockito-junit-jupiter').version('4.0.0')
        library('assertj-core', 'org.assertj', 'assertj-core').version('3.21.0')
        library('junit-pioneer', 'org.junit-pioneer', 'junit-pioneer').version('1.6.2')
        library('findsecbugs-plugin', 'com.h3xstream.findsecbugs', 'findsecbugs-plugin').version('1.11.0')
        bundle('jackson', ['jackson-databind', 'jackson-annotations', 'jackson-dataformat', 'jackson-datatype'])
        bundle('apache', ['apache-commons', 'apache-commons-lang'])
        bundle('log4j', ['log4j-api', 'log4j-core', 'log4j-impl', 'log4j-web'])
        bundle('slf4j', ['jul-to-slf4j', 'jcl-over-slf4j', 'log4j-over-slf4j'])
        bundle('junit', ['junit-jupiter-api', 'junit-jupiter-params', 'mockito-junit-jupiter'])
    }
}

Properties env = new Properties()
rootProject.file('.env').withInputStream { env.load(it) }

if (!env.containsKey('VERSION')) {
    throw new Exception('Version not specified in .env file...')
}

// `version` is used as the application build version for artifacts like jars
// `image_tag` is used as the docker tag applied to built images.
// These values are the same for building an specific Airbyte release or branch via the 'VERSION' environment variable.
// For local development builds, the 'VERSION' environment variable is unset, and built images are tagged with 'dev'.
ext {
    version = System.getenv("VERSION") ?: env.VERSION
    image_tag = System.getenv("VERSION") ?: 'dev'
}

def createLicenseWith = { File license, String startComment, String endComment, String lineComment, boolean isPython ->
    /*
    In java, we don't have a second linter/styling tool other than spotless so it doesn't really
    matter if we write a newline or not for startComment/endComment.

    However, in python, we are using black that double-checks and reformats the code.
    Thus, writing an extra empty newline (not removed by trimTrailingWhitespace() is actually a
    big deal and would be reformatted (removed) because of black's specs.
    */
    def tmp = File.createTempFile('tmp', '.tmp')
    tmp.withWriter {
        def w = it
        if (startComment.length() > 0 || !isPython) {
            w.writeLine(startComment)
        }
        license.eachLine {
            w << lineComment
            w.writeLine(it)
        }
        if (endComment.length() > 0 || !isPython) {
            w.writeLine(endComment)
        }
        w.writeLine("")
        if (isPython) {
            w.writeLine("")
        }
    }
    return tmp
}

def createPythonLicenseWith = { license ->
    return createLicenseWith(license, '', '', '', true)
}

def createJavaLicenseWith = { license ->
    return createLicenseWith(license, '/*', ' */', ' * ', false)
}

// We are the spotless exclusions rules using file tree. It seems the excludeTarget option is super finicky in a
// monorepo setup and it doesn't actually exclude directories reliably. This code makes the behavior predictable.
def createSpotlessTarget = { pattern ->
    def excludes = [
        '.gradle',
        'node_modules',
        '.eggs',
        '.mypy_cache',
        '.venv',
        '*.egg-info',
        'build',
        'dbt-project-template',
        'dbt-project-template-mssql',
        'dbt-project-template-mysql',
        'dbt-project-template-oracle',
        'dbt-project-template-clickhouse',
        'dbt-project-template-snowflake',
        'dbt_test_config',
        'normalization_test_output',
        'tools',
        'secrets',
        'charts', // Helm charts often have injected template strings that will fail general linting. Helm linting is done separately.
        'resources/seed/*_specs.yaml', // Do not remove - this is necessary to prevent diffs in our github workflows, as the file diff check runs between the Format step and the Build step, the latter of which generates the file.
    ]

    if (System.getenv().containsKey("SUB_BUILD")) {
        excludes.add("airbyte-integrations/connectors")
    }

    return fileTree(dir: rootDir, include: pattern, exclude: excludes.collect { "**/${it}" })
}

spotless {
    java {
        target createSpotlessTarget('**/*.java')

        importOrder()

        eclipse('4.21.0').configFile(rootProject.file('tools/gradle/codestyle/java-google-style.xml'))

        licenseHeaderFile createJavaLicenseWith(rootProject.file('LICENSE_SHORT'))
        removeUnusedImports()
        trimTrailingWhitespace()
    }
    groovyGradle {
        target createSpotlessTarget('**/*.gradle')
    }
    sql {
        target createSpotlessTarget('**/*.sql')

        dbeaver().configFile(rootProject.file('tools/gradle/codestyle/sql-dbeaver.properties'))
    }
    format 'styling', {
        target createSpotlessTarget(['**/*.yaml', '**/*.json'])

        prettier()
    }
}
check.dependsOn 'spotlessApply'

@SuppressWarnings('GroovyAssignabilityCheck')
def Task getDockerBuildTask(String artifactName, String projectDir, String buildVersion, String buildTag) {
    return task ("buildDockerImage-$artifactName"(type: DockerBuildImage) {
        def jdkVersion = System.getenv('JDK_VERSION') ?: '17.0.1'

        def arch = System.getProperty("os.arch").toLowerCase()
        def isArm64 = arch == "aarch64" || arch == "arm64"

        def buildPlatform = System.getenv('DOCKER_BUILD_PLATFORM') ?: isArm64 ? 'linux/arm64' : 'linux/amd64'
        def alpineImage = System.getenv('ALPINE_IMAGE') ?: isArm64 ? 'arm64v8/alpine:3.14' : 'amd64/alpine:3.14'
        def buildArch = System.getenv('DOCKER_BUILD_ARCH') ?: isArm64 ? 'arm64' : 'amd64'

        inputDir = file("$projectDir/build/docker")
        platform = buildPlatform
        images.add("airbyte/$artifactName:$buildTag")
        buildArgs.put('JDK_VERSION', jdkVersion)
        buildArgs.put('DOCKER_BUILD_ARCH', buildArch)
        buildArgs.put('ALPINE_IMAGE', alpineImage)
        buildArgs.put('VERSION', buildVersion)
    })
}

allprojects {
    apply plugin: 'com.bmuschko.docker-remote-api'

    task copyDocker(type: Sync) {
        from "${project.projectDir}/Dockerfile"
        into "build/docker/"
    }
}

allprojects {
    apply plugin: 'base'

    // by default gradle uses directory as the project name. That works very well in a single project environment but
    // projects clobber each other in an environments with subprojects when projects are in directories named identically.
    def sub = rootDir.relativePath(projectDir.parentFile).replace('/', '.')
    group = "io.${rootProject.name}${sub.isEmpty() ? '' : ".$sub"}"
    project.archivesBaseName = "${project.group}-${project.name}"

    version = rootProject.ext.version
}

// Java projects common configurations
subprojects {

    if (project.name == 'airbyte-webapp' || project.name == 'airbyte-webapp-e2e-tests') {
        return
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven {
            // TODO(Issue-4915): Remove this when upstream is merged in.
            url 'https://airbyte.mycloudrepo.io/public/repositories/airbyte-public-jars/'
        }
    }

    pmd {
        consoleOutput = true

        rulesMinimumPriority = 5
        ruleSets = []
        ruleSetFiles = files(rootProject.file('tools/gradle/pmd/rules.xml'))
    }

    def integrationTagName = 'platform-integration'
    def slowIntegrationTagName = 'platform-slow-integration'
    // make tag accessible in submodules.
    ext {
        cloudStorageTestTagName = 'cloud-storage'
    }

    test {
        useJUnitPlatform {
            excludeTags(integrationTagName, slowIntegrationTagName, cloudStorageTestTagName)
        }
        testLogging() {
            events 'failed'
            exceptionFormat 'full'
            // showStandardStreams = true
        }
        finalizedBy jacocoTestReport
    }

    task newIntegrationTests(type: Test) {
        useJUnitPlatform {
            includeTags integrationTagName
        }
        testLogging() {
            events 'failed'
            exceptionFormat 'full'
        }
        finalizedBy jacocoTestReport
    }

    task slowIntegrationTests(type: Test) {
        useJUnitPlatform {
            includeTags slowIntegrationTagName
        }
        testLogging() {
            events 'failed'
            exceptionFormat 'full'
        }
        finalizedBy jacocoTestReport
    }

    task allTests(type: Test) {
        useJUnitPlatform()
        testLogging() {
            events 'failed'
            exceptionFormat 'full'
        }
        finalizedBy jacocoTestReport
    }

    spotbugs {
        reportLevel = 'high'
        excludeFilter = rootProject.file('spotbugs-exclude-filter-file.xml')
    }

    dependencies {
        if (project.name != 'airbyte-commons') {
            implementation project(':airbyte-commons')
        }

        implementation(platform(libs.fasterxml))
        implementation(platform(libs.glassfish))


        // version is handled by "com.fasterxml.jackson:jackson-bom:2.10.4", so we do not explicitly set it here.
        implementation libs.bundles.jackson

        implementation libs.guava

        implementation libs.commons.io

        implementation libs.bundles.apache

        implementation libs.slf4j.api


        // SLF4J as a facade over Log4j2 required dependencies
        implementation libs.bundles.log4j

        // Bridges from other logging implementations to SLF4J
        implementation libs.bundles.slf4j

        // Dependencies for logging to cloud storage, as well as the various clients used to do so.
        implementation libs.appender.log4j2
        implementation libs.aws.java.sdk.s3
        implementation libs.google.cloud.storage

        implementation libs.s3

        // Lombok dependencies
        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        testRuntimeOnly libs.junit.jupiter.engine
        testImplementation libs.bundles.junit
        testImplementation libs.assertj.core

        testImplementation libs.junit.pioneer

        // adds owasp plugin
        spotbugsPlugins libs.findsecbugs.plugin
    }

    tasks.withType(Tar) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }

    tasks.withType(Zip) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }

    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}

task('generate') {
    dependsOn subprojects.collect { it.getTasksByName('generateProtocolClassFiles', true) }
    dependsOn subprojects.collect { it.getTasksByName('generateJsonSchema2Pojo', true) }
}

task('format') {
    dependsOn generate
    dependsOn spotlessApply
    dependsOn subprojects.collect { it.getTasksByName('airbytePythonFormat', true) }
}

// add licenses for python projects.
subprojects {
    def pythonFormatTask = project.tasks.findByName('blackFormat')

    if (pythonFormatTask != null) {
        apply plugin: "com.github.hierynomus.license"
        task licenseFormatPython(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
            header = createPythonLicenseWith(rootProject.file('LICENSE_SHORT'))
            source = fileTree(dir: projectDir)
                    .include("**/*.py")
                    .exclude(".venv/**/*.py")
                    .exclude("**/airbyte_api_client/**/*.py")
                    .exclude("**/__init__.py")
            strictCheck = true
        }
        def licenseTask = project.tasks.findByName('licenseFormatPython')
        blackFormat.dependsOn licenseTask
        isortFormat.dependsOn licenseTask
        flakeCheck.dependsOn licenseTask

        def generateFilesTask = project.tasks.findByName('generateProtocolClassFiles')
        if (generateFilesTask != null) {
            licenseTask.dependsOn generateFilesTask
        }
    }
}

task('generate-docker') {
    dependsOn(':airbyte-bootloader:assemble')
    dependsOn(':airbyte-workers:assemble')
    dependsOn(':airbyte-webapp:assemble')
    dependsOn(':airbyte-server:assemble')
    dependsOn(':airbyte-scheduler:app:assemble')
    dependsOn(':airbyte-db:lib:assemble')
    dependsOn(':airbyte-config:init:assemble')
    dependsOn(':airbyte-temporal:assemble')
}

// produce reproducible archives
// (see https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives)
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// TODO: Davin will turn this back on.
//// Configure what subprojects to publish.
//String[] toPublish = [
//        ':airbyte-analytics',
//        ':airbyte-api',
//        ':airbyte-commons',
//        ':airbyte-commons-docker',
//        ':airbyte-config:init',
//        ':airbyte-config:models',
//        ':airbyte-config:persistence',
//        ':airbyte-db:lib',
//        ':airbyte-json-validation',
//        ':airbyte-notification',
//        ':airbyte-protocol:models',
//        ':airbyte-scheduler:client',
//        ':airbyte-scheduler:models',
//        ':airbyte-scheduler:persistence',
//        ':airbyte-server',
//        ':airbyte-workers'
//]
//configure(subprojects.findAll { toPublish.contains(it.getPath()) }) {
//    apply plugin: 'maven-publish'
//
//    publishing {
//            publications {
//                // This block is present so Gradle knows to publish a Maven jar.
//                maven(MavenPublication) {
//                    from components.java
//                    from components.versionCatalog
//                    // Gradle will by default use the subproject path as the group id and the subproject name as the artifact id.
//                    // e.g. the subproject :airbyte-scheduler:models is imported at io.airbyte.airbyte-config:persistence:<version-number>.
//                }
//            }
//
//            repositories {
//                maven {
//                    url 'https://airbyte.mycloudrepo.io/repositories/airbyte-public-jars'
//                    credentials {
//                        name 'cloudrepo'
//                        username System.getenv('CLOUDREPO_USER')
//                        password System.getenv('CLOUDREPO_PASSWORD')
//                    }
//                }
//
//            mavenLocal()
//        }
//    }
//}
