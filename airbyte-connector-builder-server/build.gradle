import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

/*
TODOs for making incremental: 
1. make generateOpenApiPythonServer incremental 
    1a. Verify if we need to make the copy task inputs/outputs explicitly declared
2. make airbyte-python-docker incremental
3. make airbyte-docker incremental
4. make anything else incremental

Bonus: attach unit tests to the Gradle-defined project “test” task 

*/


plugins {
    id "org.openapi.generator" version "6.3.0"
    id 'airbyte-python-docker'
    id 'airbyte-docker'
}

task generateOpenApiPythonServer(type: GenerateTask) {
    def generatedCodeDir = "$buildDir/airbyte_connector_builder_server"
    // TODO this openapi file doesn't need to be nested in 3 empty folders
    inputSpec = "$rootDir.absolutePath/airbyte-connector-builder-server/src/main/openapi/openapi.yaml"
    generatorName = "python-fastapi"
    configFile = "$projectDir/openapi/generator_config.yaml"
    templateDir = "$projectDir/openapi/templates"
    packageName = "connector_builder.generated"

    outputDir = generatedCodeDir
    // After we generate, we're only interested in the API declaration and the generated pydantic models.
    // So we copy those from the build/ directory
    // TODO: should we define these as inputs/outputs? Should the copy even be a separate task that depends 
    // on this task?
    doLast {
        def sourceDir = "$generatedCodeDir/src/connector_builder/generated/"
        def targetDir = "$projectDir/connector_builder/generated"
        mkdir targetDir
        copy {
            from "$sourceDir/apis"
            include "*_interface.py", "__init__.py"
            into "$targetDir/apis"
        }
        copy {
            from "$sourceDir/models"
            include "*.py"
            into "$targetDir/models"
        }
    }
}

// it's important to explicitly say that the AirbyteDocker task (which takes as input all files in this directory)
// dependsOn any task which manipulates files in this directory, or else Gradle will disable incremental builds on those
// tasks to preserve build correctness. More info at https://docs.gradle.org/7.6/userguide/validation_problems.html#implicit_dependency
airbyteDocker.dependsOn(generateOpenApiPythonServer)
project.build.dependsOn(generateOpenApiPythonServer)

// java modules such as airbyte-server can use copyGeneratedTar to copy the files to the docker image
// We cannot do this here because we don't generate a tar file
// Instead, we copy the files into the build directory so they can be copied to the docker container
task prepareBuild(type: Copy) {
    from layout.projectDirectory.file(".")
    exclude '.*'
    exclude 'build'
    exclude '**/*.pyc'

    into layout.buildDirectory.dir("docker")
}

tasks.named("buildDockerImage") {
    dependsOn prepareBuild
    dependsOn copyDocker
}
