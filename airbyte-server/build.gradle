plugins {
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

// This is published as a shadow jar as the Airbyte Server jar is currently used
// as a delivery mechanism for the common Airbyte libraries and clients. Proper
// publishing is blocked on some gradle work.
shadowJar {
    // following properties need to be set here to have reproducible shadow archives, doesn't seem to inherit from AbstractArchiveTask
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    zip64 true
    mergeServiceFiles()
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    // Not stubbing this out adds 'all' to the end of the jar's name.
    classifier = ''
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        publications {
            // This block is present so Gradle knows to publish a Maven jar.
            maven(MavenPublication) {
                from components.java
                // Gradle will by default use the subproject path as the group id and the subproject name as the artifact id.
                // e.g. the subproject :airbyte-scheduler:models is imported at io.airbyte.airbyte-config:persistence:<version-number>.
            }
        }

        maven {
            credentials {
                name 'cloudrepo'
                username System.getenv('CLOUDREPO_USER')
                password System.getenv('CLOUDREPO_PASSWORD')
            }
            url 'https://airbyte.mycloudrepo.io/repositories/airbyte-public-jars'
        }

        mavenLocal()
    }
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:${rootProject.ext.micronaut_version}")
    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut:micronaut-management'
    annotationProcessor 'io.micronaut:micronaut-validation'
    annotationProcessor 'io.micronaut.data:micronaut-data-processor:3.3.0'
    annotationProcessor 'io.micronaut.jaxrs:micronaut-jaxrs-processor:3.2.0'

    implementation platform("io.micronaut:micronaut-bom:${rootProject.ext.micronaut_version}")
    implementation 'io.micronaut:micronaut-inject'
    implementation 'io.swagger.core.v3:swagger-annotations'
    implementation 'io.micronaut:micronaut-validation'
    implementation 'io.micronaut:micronaut-runtime'
    implementation 'io.micronaut:micronaut-http-server-netty'
    implementation 'io.micronaut:micronaut-http-client'
    implementation 'io.micronaut:micronaut-management'
    implementation 'io.micronaut.security:micronaut-security:3.5.0'
    implementation 'io.micronaut.jaxrs:micronaut-jaxrs-server:3.2.0'
    implementation 'io.micronaut.data:micronaut-data-hibernate-jpa:3.3.0'
    implementation 'io.micronaut.sql:micronaut-jdbc-hikari'

    implementation 'com.vladmihalcea:hibernate-types-52:2.15.0'

    implementation 'javax.inject:javax.inject:1'
    implementation 'io.projectreactor:reactor-core:3.4.16'

    implementation 'io.temporal:temporal-sdk:1.8.1'

    implementation 'org.apache.cxf:cxf-core:3.4.2'
    implementation 'commons-cli:commons-cli:1.4'
    implementation "org.flywaydb:flyway-core:7.14.0"
    implementation 'com.github.slugify:slugify:2.4'

    implementation project(':airbyte-analytics')
    implementation project(':airbyte-api')
    implementation project(':airbyte-commons-docker')
    implementation project(':airbyte-config:init')
    implementation project(':airbyte-config:models')
    implementation project(':airbyte-config:persistence')
    implementation project(':airbyte-config:specs')
    implementation project(':airbyte-db:lib')
    implementation project(":airbyte-json-validation")
    implementation project(':airbyte-notification')
    implementation project(':airbyte-oauth')
    implementation project(':airbyte-protocol:models')
    implementation project(':airbyte-scheduler:app')
    implementation project(':airbyte-scheduler:client')
    implementation project(':airbyte-scheduler:models')
    implementation project(':airbyte-scheduler:persistence')
    implementation project(':airbyte-workers')

    testImplementation "org.postgresql:postgresql:42.2.18"

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'

    testImplementation "org.testcontainers:postgresql:1.15.3"
}

// we want to be able to access the generated db files from config/init when we build the server docker image.
task copySeed(type: Copy, dependsOn: [project(':airbyte-config:init').processResources]) {
    from "${project(':airbyte-config:init').buildDir}/resources/main/config"
    into "${buildDir}/config_init/resources/main/config"
}

// need to make sure that the files are in the resource directory before copying.
// tests require the seed to exist.
test.dependsOn(project.tasks.copySeed)
assemble.dependsOn(project.tasks.copySeed)

application {
//    mainClass = 'io.airbyte.api.invoker.Application'
    mainClass = 'io.airbyte.server.Application'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

Properties env = new Properties()
rootProject.file('.env.dev').withInputStream { env.load(it) }

run {
    // default for running on local machine.
    environment 'DATABASE_USER', env.DATABASE_USER
    environment 'DATABASE_PASSWORD', env.DATABASE_PASSWORD
    environment 'DATABASE_URL', env.DATABASE_URL
    environment 'JOBS_DATABASE_MINIMUM_FLYWAY_MIGRATION_VERSION', env.JOBS_DATABASE_MINIMUM_FLYWAY_MIGRATION_VERSION
    environment 'JOBS_DATABASE_INITIALIZATION_TIMEOUT_MS', env.JOBS_DATABASE_INITIALIZATION_TIMEOUT_MS

    environment 'CONFIG_DATABASE_USER', env.DATABASE_USER
    environment 'CONFIG_DATABASE_PASSWORD', env.DATABASE_PASSWORD
    environment 'CONFIG_DATABASE_URL', env.DATABASE_URL
    environment 'CONFIGS_DATABASE_MINIMUM_FLYWAY_MIGRATION_VERSION', env.CONFIGS_DATABASE_MINIMUM_FLYWAY_MIGRATION_VERSION
    environment 'CONFIGS_DATABASE_INITIALIZATION_TIMEOUT_MS', env.CONFIGS_DATABASE_INITIALIZATION_TIMEOUT_MS

    environment 'RUN_DATABASE_MIGRATION_ON_STARTUP', 'true'

    environment 'WORKSPACE_ROOT', env.WORKSPACE_ROOT
    environment 'CONFIG_ROOT', '/tmp/airbyte_config'
    environment 'TRACKING_STRATEGY', env.TRACKING_STRATEGY
    environment 'AIRBYTE_VERSION', env.VERSION
    environment 'AIRBYTE_ROLE', System.getenv('AIRBYTE_ROLE')
    environment 'TEMPORAL_HOST', env.TEMPORAL_HOST
    environment 'WEBAPP_URL', env.WEBAPP_URL
    environment 'WORKSPACE_ROOT', env.WORKSPACE_ROOT

    environment 'SERVICE_NAME', project.name
    environment 'MICRONAUT_ENVIRONMENTS', 'local'
}

task copyGeneratedTar(type: Copy) {
    dependsOn copyDocker
    dependsOn distTar

    from('build/distributions') {
        include 'airbyte-server-*.tar'
    }
    into 'build/docker/bin'
}

Task dockerBuildTask = getDockerBuildTask("server", "$project.projectDir")
dockerBuildTask.dependsOn(copyGeneratedTar)
assemble.dependsOn(dockerBuildTask)

// produce reproducible archives
// (see https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives)
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
