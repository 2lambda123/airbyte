/*
Gradle plugin for Java-based Airbyte connectors.
Also facilitates importing and working with the Java CDK.
*/

import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbyteJavaConnectorExtension {

    boolean useCdkProjectRef = false
    String cdkVersionRequired
    List<String> features = []  // e.g. 'db-sources', 'db-destinations'
    Project project

    AirbyteJavaConnectorExtension(Project project) {
        this.project = project
    }

    void addCdkDependencies() {
        // Create a list of CDK submodules to import
        def submoduleNames = ['core']
        features.each { feature ->
            submoduleNames.add("${feature}-feature")
        }
        if (useCdkProjectRef) {
            project.dependencies {
                submoduleNames.each { submoduleName ->
                    // Add the CDK module to the dependencies
                    def cdkModule = project.project(":airbyte-cdk:java:airbyte-cdk:${submoduleName}")
                    implementation cdkModule
                    testImplementation cdkModule
                    integrationTestJavaImplementation cdkModule
                    performanceTestJavaImplementation cdkModule

                    // Add the test fixtures cdkModule to the dependencies.
                    // Exclude 'core', which has no test fixtures.
                    if (cdkModule != 'core') {
                        def testFixturesRef = testFixtures(project.project(":airbyte-cdk:java:airbyte-cdk:${submoduleName}"))
                        testImplementation testFixturesRef
                        integrationTestJavaImplementation testFixturesRef
                        performanceTestJavaImplementation testFixturesRef
                    }
                }
            }
        } else {
            project.dependencies {
                submoduleNames.each { submoduleName ->
                    // Add the cdkModule to the dependencies
                    def cdkModule = "io.airbyte:airbyte-cdk-${submoduleName}:${cdkVersionRequired}"
                    implementation cdkModule
                    testImplementation cdkModule
                    integrationTestJavaImplementation cdkModule
                    performanceTestJavaImplementation cdkModule

                    // Add the test fixtures cdkModule to the dependencies.
                    // Exclude 'core', which has no test fixtures.
                    if (cdkModule != 'core') {
                        def testFixturesRef = "io.airbyte:airbyte-cdk-${submoduleName}-test-fixtures:${cdkVersionRequired}"
                        testImplementation testFixturesRef
                        integrationTestJavaImplementation testFixturesRef
                        performanceTestJavaImplementation testFixturesRef
                    }
                }
            }
        }
    }
}


class AirbyteJavaConnectorPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {
        // def cdkTargetVersion = project.ext.getCdkTargetVersion(project)
        def extension = project.extensions.create('airbyteJavaConnector', AirbyteJavaConnectorExtension, project)

        project.plugins.apply(AirbyteJavaCdkPlugin)
        project.plugins.apply(AirbyteIntegrationTestJavaPlugin)
        project.plugins.apply(AirbytePerformanceTestJavaPlugin)
        project.plugins.apply(AirbyteConnectorAcceptanceTestPlugin)

        project.dependencies {
            // Integration and performance tests should automatically
            // have access to the project's own main source sets.
            integrationTestJavaImplementation project
            performanceTestJavaImplementation project
        }
    }
}
