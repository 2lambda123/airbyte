plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'io.airbyte'

// Version is dynamically loaded from version.properties file.
def props = new Properties()
file("src/main/resources/version.properties").withInputStream { props.load(it) }
version = props.getProperty('version')
description = "Airbyte Connector Development Kit (CDK) for Java."

repositories {
    mavenLocal()
    mavenCentral()
    // Should not be needed:
    // maven { url 'https://airbyte.mycloudrepo.io/public/repositories/airbyte-public-jars' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.airbyte'
            artifactId = 'airbyte-cdk'
            from components.java
        }
    }
    repositories {
        maven {
            name 'cloudrepo'
            url 'https://airbyte.mycloudrepo.io/repositories/airbyte-public-jars'
            credentials {
                username System.getenv('CLOUDREPO_USER')
                password System.getenv('CLOUDREPO_PASSWORD')
            }
        }
    }
}

// Ensures that connectors can use snapshot versions of the CDK in MavenLocal.
build.dependsOn(publishToMavenLocal)
// Confirm local publish status by running:
// $ ls -la ~/.m2/repository/io/airbyte/airbyte-cdk*
// Confirm jar contents by running:
// $ jar tf ~/.m2/repository/io/airbyte/airbyte-cdk/0.0.2-SNAPSHOT/airbyte-cdk-0.0.2-SNAPSHOT.jar
// Remove CDK artifacts from MavenLocal by running:
// $ rm -rf ~/.m2/repository/io/airbyte/airbyte-cdk*

publishToMavenLocal {
    // Get the path to the artifact in the local Maven repository
    def artifactPath = "${System.getProperty('user.home')}/.m2/repository/io/airbyte/airbyte-cdk/${version}/airbyte-cdk-${version}.jar"

    // Always re-publish the artifact to MavenLocal
    outputs.upToDateWhen { false }

    doFirst {
        println "Running CDK publishToMavenLocal..."
    }
    doLast {
        println "Finished CDK publishToMavenLocal."
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showExceptions = true
        showCauses = true
        showStackTraces = false
        events = ['passed', 'skipped', 'failed']
    }
}
