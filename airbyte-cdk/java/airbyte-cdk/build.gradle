plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'io.airbyte'

// Version is dynamically loaded from version.properties file.
def props = new Properties()
file("src/main/resources/version.properties").withInputStream { props.load(it) }
version = props.getProperty('version')
description = "Airbyte Connector Development Kit (CDK) for Java."

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.airbyte'
            artifactId = 'airbyte-cdk'
            from components.java
        }
    }
    repositories {
        maven {
            name 'cloudrepo'
            url 'https://airbyte.mycloudrepo.io/repositories/airbyte-public-jars'
            credentials {
                username System.getenv('CLOUDREPO_USER')
                password System.getenv('CLOUDREPO_PASSWORD')
            }
        }
    }
}

// Adds publishToMavenLocal as final command in the list of 'build' tasks.
build.dependsOn(publishToMavenLocal)

publishToMavenLocal {
    // Always re-publish the artifact to MavenLocal
    outputs.upToDateWhen { false }

    doFirst {
        println "Running CDK publishToMavenLocal..."
    }
    doLast {
        println "Finished CDK publishToMavenLocal."
    }
}
// MavenLocal debugging steps:
// 1. Confirm local publish status by running:
//    ls -la ~/.m2/repository/io/airbyte/airbyte-cdk*
// 2. Confirm jar contents by running:
//    jar tf ~/.m2/repository/io/airbyte/airbyte-cdk/0.0.2-SNAPSHOT/airbyte-cdk-0.0.2-SNAPSHOT.jar
// 3. Remove CDK artifacts from MavenLocal by running:
//    rm -rf ~/.m2/repository/io/airbyte/airbyte-cdk*
// 4. Rebuid CDK artifacts by running:
//    ./gradlew :airbyte-cdk:java:airbyte-cdk:build
//    or
//    ./gradlew :airbyte-cdk:java:airbyte-cdk:publishToMavenLocal

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showExceptions = true
        showCauses = true
        showStackTraces = false
        events = ['passed', 'skipped', 'failed']
    }
}
//----------------------------------------
// TODO: Merge imports from build.gradle of 'base-java':
//----------------------------------------
// plugins {
//     id 'java-library'
//     id 'airbyte-docker'
// }

// dependencies {
//     implementation libs.airbyte.protocol
//     implementation project(':airbyte-config-oss:config-models-oss')
//     implementation project(':airbyte-commons-cli')
//     implementation project(':airbyte-json-validation')

//     implementation 'commons-cli:commons-cli:1.4'
//     implementation 'net.i2p.crypto:eddsa:0.3.0'
//     implementation 'org.apache.sshd:sshd-mina:2.8.0'
//     // bouncycastle is pinned to version-match the transitive dependency from kubernetes client-java
//     // because a version conflict causes "parameter object not a ECParameterSpec" on ssh tunnel initiation
//     implementation 'org.bouncycastle:bcprov-jdk15on:1.66'
//     implementation 'org.bouncycastle:bcpkix-jdk15on:1.66'
//     implementation 'org.bouncycastle:bctls-jdk15on:1.66'

//     implementation libs.jackson.annotations
//     implementation libs.connectors.testcontainers
//     implementation libs.connectors.testcontainers.jdbc
//     implementation libs.bundles.datadog

//     implementation files(project(':airbyte-integrations:bases:base').airbyteDocker.outputs)

//     testImplementation 'commons-lang:commons-lang:2.6'
//     implementation group: 'org.apache.logging.log4j', name: 'log4j-layout-template-json', version: '2.17.2'
// }
