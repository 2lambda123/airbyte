plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'io.airbyte'

// Version is dynamically loaded from version.properties file.
def props = new Properties()
file("src/main/resources/version.properties").withInputStream { props.load(it) }
version = props.getProperty('version')
description = "Airbyte Connector Development Kit (CDK) for Java."

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.airbyte'
            artifactId = 'airbyte-cdk'
            from components.java
        }
    }
    repositories {
        maven {
            name 'cloudrepo'
            url 'https://airbyte.mycloudrepo.io/repositories/airbyte-public-jars'
            credentials {
                username System.getenv('CLOUDREPO_USER')
                password System.getenv('CLOUDREPO_PASSWORD')
            }
        }
    }
}

// Adds publishToMavenLocal as final command in the list of 'build' tasks.
build.dependsOn(publishToMavenLocal)

publishToMavenLocal {
    // Always re-publish the artifact to MavenLocal
    outputs.upToDateWhen { false }

    doFirst {
        println "Running CDK publishToMavenLocal..."
    }
    doLast {
        println "Finished CDK publishToMavenLocal."
    }
}

// This task will be a no-op if CDK version does not end with '-SNAPSHOT'.
task publishLocalCdkSnapshot {}

if (version.endsWith("-SNAPSHOT")) {
    logger.lifecycle("Version ${version} ends with '-SNAPSHOT'. Enqueing 'publishToMavenLocal'...")
    publishLocalCdkSnapshot.dependsOn publishToMavenLocal
} else {
    logger.lifecycle("Version ${version} does not end with '-SNAPSHOT'. Skipping task 'publishToMavenLocal'.")
}

// MavenLocal debugging steps:
// 1. Confirm local publish status by running:
//    ls -la ~/.m2/repository/io/airbyte/airbyte-cdk*
// 2. Confirm jar contents by running:
//    jar tf ~/.m2/repository/io/airbyte/airbyte-cdk/0.0.2-SNAPSHOT/airbyte-cdk-0.0.2-SNAPSHOT.jar
// 3. Remove CDK artifacts from MavenLocal by running:
//    rm -rf ~/.m2/repository/io/airbyte/airbyte-cdk*
// 4. Rebuid CDK artifacts by running:
//    ./gradlew :airbyte-cdk:java:airbyte-cdk:build
//    or
//    ./gradlew :airbyte-cdk:java:airbyte-cdk:publishToMavenLocal

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showExceptions = true
        showCauses = true
        showStackTraces = false
        events = ['passed', 'skipped', 'failed']
    }
}
