import org.jsonschema2pojo.SourceType

plugins {
    id "java-library"
    id 'com.github.eirnym.js2p' version '1.0'
}

group 'io.airbyte...'
version '0.40.14'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor platform(libs.micronaut.bom)
    annotationProcessor libs.bundles.micronaut.annotation.processor

    implementation platform(libs.micronaut.bom)
    implementation libs.bundles.micronaut

    implementation 'io.fabric8:kubernetes-client:5.12.2'
    implementation 'io.temporal:temporal-sdk:1.8.1'
    implementation 'org.apache.ant:ant:1.10.10'
    implementation 'org.apache.commons:commons-text:1.9'

    implementation project(':airbyte-commons-protocol')
    implementation project(':airbyte-commons-temporal')
    implementation project(':airbyte-config:config-models')
    implementation project(':airbyte-json-validation')
    implementation project(':airbyte-metrics:metrics-lib')
    implementation project(':airbyte-persistence:job-persistence')
    implementation project(':airbyte-protocol:protocol-models')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/workers_models")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')
    removeOldOutput = true

    targetPackage = 'io.airbyte.persistence.job.models'

    useLongIntegers = true
    generateBuilders = true
    includeConstructors = false
    includeSetters = true
}

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)
